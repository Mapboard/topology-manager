#!/usr/bin/env coffee
yargs = require 'yargs'
{spawnSync} = require 'child_process'
{join} = require 'path'
{existsSync} = require 'fs'
colors = require 'colors'

external = (cmd, describe="")->
  prefixedCommand = "geologic-map-#{cmd}"
  command = cmd

  handler = (argv)->
    # See if this is available locally, if not, search
    # on path.
    fp = join __dirname, prefixedCommand
    prefixedCommand = fp if existsSync fp
    spawnSync prefixedCommand, argv._.slice(1), {stdio: 'inherit'}
  return {command, describe, handler}

argv = yargs
  .usage '$0 <command>'
  .option 'c', {
    alias: 'config',
    description: "JSON config file. Used in lieu of the
                  GEOLOGIC_MAP_CONFIG environment variable"}
  .command external 'create-tables', 'Create database tables and fixtures'
  .command external 'set-colors [file]', 'Set colors from csv file (id,color)'
  .command external 'config [sub.item]', 'Show configuration, optionally fetching a specific value'
  .command require '../src/commands/delete'
  .command require '../src/commands/reset'
  .demandCommand()
  .argv

